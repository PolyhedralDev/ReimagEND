num randPrecision = 100;

num length = 25 + randomInt(randPrecision)/randPrecision*10;
num radius = 4 + randomInt(randPrecision)/randPrecision;
num yTranslate = 5;


str setBlockA = "minecraft:green_stained_glass";
str setBlockB = "minecraft:verdant_froglight";

// Direction vector
num dx = (randomInt(randPrecision)/randPrecision-0.5);
num dy = -0.5;
num dz = (randomInt(randPrecision)/randPrecision-0.5);

// Normalize vector
num mag = sqrt(pow(dx,2)+pow(dy,2)+pow(dz,2));
dx = dx/mag;
dy = dy/mag;
dz = dz/mag;

for(num l = 0; l <= length; l = l + 1) {

    // Reduce radius inversely to length
    num r = (radius/length * -l) + radius;

    // Generate sphere at each point
    for(num x = -r; x <= r; x = x + 1) {
        for(num y = -r; y <= r; y = y + 1) {
            for(num z = -r; z <= r; z = z + 1) {
                if (pow(x,2)+pow(y,2)+pow(z,2) < pow(r,2)) {
                    setBlockA = "minecraft:green_stained_glass";
                    if (randomInt(4+l) == 0) setBlockA = "minecraft:lime_stained_glass";
                        block(l*dx + x, l*dy + y - radius + yTranslate, l*dz + z, setBlockA, false);
                }

                if ((pow(x,2))+(pow(y,2)*2)+(pow(z,2)) < (pow(r,2)-10)) {
                        block(l*dx + x, l*dy + y - radius + yTranslate, l*dz + z,setBlockB, true);
                }

                
            }
        }
    }
}