num radius = 2 + randomInt(1);
num length = 20 + randomInt(15);
str mainTendrilBlock = "minecraft:purple_concrete";
str altTendrilBlock = "minecraft:purple_concrete_powder";
num endBuffer = length * 0.5;

num yDir = 1;
if (getBlock(0,1,0) != "minecraft:air") yDir = -1;

str setBlock = mainTendrilBlock;
num xOffset = 0;
num zOffset = 0;
for (num l = 0; l <= length - endBuffer; l = l + 1) {

    // Reduce radius inversely to length
    num r = (radius / length * -l) + radius;

    // Generate sphere at each point
    for (num x = -r; x <= r; x = x + 1) {
        for (num y = -r; y <= r; y = y + 1) {
            for (num z = -r; z <= r; z = z + 1) {
                if (pow(x, 2) + pow(y, 2) + pow(z, 2) < pow(r, 2)) {
                    setBlock = mainTendrilBlock;
                    if (randomInt(2) == 0) {
                        setBlock = altTendrilBlock;
                        block(x + xOffset, (l * yDir) + y - radius - 1, z + zOffset, mainTendrilBlock, true);
                    }
                    block(x + xOffset, (l * yDir) + y - radius, z + zOffset, setBlock, true);
                }
            }
        }
    }

    xOffset = xOffset + (1 - randomInt(3));
    zOffset = zOffset + (1 - randomInt(3));

}
