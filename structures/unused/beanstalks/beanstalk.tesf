num length = 40;
num maxRadius = 10;
num radius = 3;
num amount = 1;

num leafInterval = 10;

num yTranslate = -5;
num xOffset = 0;
num zOffset = 0;

if (getBlock(3,yTranslate,0) == "minecraft:air" ||
    getBlock(0,yTranslate,3) == "minecraft:air" ||
    getBlock(-3,yTranslate,0) == "minecraft:air" ||
    getBlock(0,yTranslate,-3) == "minecraft:air" ) fail; 

str setBlock = "";
num random = 0;
for (num a = 0; a < amount; a = a + 1) {
    for (num l = 0; l < length; l = l + 1) {
        
        if (l % leafInterval == 0 && 
            l < (length-5) &&
            l > 5) structure(xOffset*1.2, l + yTranslate, zOffset*1.2, "beanstalk_leaf", "NONE", "CW_90", "CW_180", "CCW_90");

        if (l % 5 == 0) radius = radius*0.92;
        for (num x = -radius; x < radius; x = x + 1) {
            for (num y = -radius; y < radius; y = y + 1) {
                for (num z = -radius; z < radius; z = z + 1) {

                    if (pow(x,2)+pow(y,2)+pow(z,2) < pow(radius,2)) {

                        block(x+xOffset, y + l + yTranslate, z+zOffset,"minecraft:moss_block",false);
                    }
                }   
            }
        }

        random = randomInt(4);
        if(random == 0) {
            if (xOffset < maxRadius) {
                xOffset = xOffset + 1;
            } else xOffset = xOffset - 1;
        } else if(random == 1) {
            if (xOffset > -maxRadius) {
                xOffset = xOffset - 1;
            } else xOffset = xOffset + 1;
        } else if(random == 2) {
            if (zOffset < maxRadius) {
                zOffset = zOffset + 1;
            } else zOffset = zOffset - 1;
        } else if(random == 3) {
            if (zOffset > -maxRadius) {
                zOffset = zOffset - 1;
            } else zOffset = zOffset + 1;
        }

    }

    xOffset = 0;
    zOffset = 0;
}
